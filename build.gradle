buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath('org.robolectric:robolectric-gradle-plugin:0.14.1') {
            // These two generate dependency warnings against android's gradle plugin, so exclude them here:
            exclude group: 'commons-logging', module: 'commons-logging'
            exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'robolectric'

repositories {
    mavenLocal()
    mavenCentral()
}

android {
    defaultConfig {
        applicationId 'com.meetme.android.robotest.aar'
        versionCode 1
        versionName '1'
        compileSdkVersion 21
        targetSdkVersion 18
        minSdkVersion 11
        buildToolsVersion '21.1.2'
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
        }
        debug {
            debuggable true
            minifyEnabled false
        }
        internal.initWith(buildTypes.release)
        internal {
            debuggable true
        }
    }

    sourceSets {
        androidTest.setRoot('src/test')
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.+'

    androidTestCompile 'junit:junit:4.10'
    androidTestCompile 'org.robolectric:robolectric:2.4'
    androidTestCompile 'org.assertj:assertj-core:1.6.1'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0@aar'
    androidTestCompile 'com.squareup.assertj:assertj-android-support-v4:1.0.0@aar'
}

tasks.withType(Test) {
    testLogging.exceptionFormat = 'full'
}